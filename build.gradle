plugins {
	id 'java'
	id 'dev.architectury.loom' version '1.11-SNAPSHOT' apply false
	id 'architectury-plugin' version '3.4-SNAPSHOT'
	id 'com.github.johnrengelman.shadow' version '8.1.1' apply false
	id 'maven-publish'
	id "com.modrinth.minotaur" version "2.+" apply false
	id 'net.darkhax.curseforgegradle' version '1.1.15' apply false
}

architectury {
	minecraft = project.minecraft_version
}

version = project.mod_version
group = project.maven_group

// 排除根项目的源代码
sourceSets {
	main {
		java {
			srcDirs = []
		}
		resources {
			srcDirs = []
		}
	}
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'dev.architectury.loom'
	apply plugin: 'architectury-plugin'
	apply plugin: 'maven-publish'
	
	base {
		archivesName = "${rootProject.archives_base_name}-${project.name}"
	}
	
	repositories {
		maven {
			name = 'Modrinth'
			url = "https://api.modrinth.com/maven"
		}
		maven {
			name = 'Architectury'
			url = 'https://maven.architectury.dev/'
		}
		mavenCentral()
		maven {
			name = 'NeoForged'
			url = 'https://maven.neoforged.net/releases/'
		}
	}
	
	dependencies {
		minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
		
		// 统一使用 Yarn + NeoForge 补丁映射（所有平台）
		mappings loom.layered {
			it.mappings("net.fabricmc:yarn:${rootProject.yarn_mappings}:v2")
			it.mappings("dev.architectury:yarn-mappings-patch-neoforge:${rootProject.yarn_mappings_patch_neoforge_version}")
		}
	}
	
	java {
		withSourcesJar()
		sourceCompatibility = JavaVersion.VERSION_21
		targetCompatibility = JavaVersion.VERSION_21
	}
	
	tasks.withType(JavaCompile).configureEach {
		it.options.release = 21
	}
	
	processResources {
		inputs.property "version", project.version
	}
	
	publishing {
		publications {
			create("mavenJava", MavenPublication) {
				artifactId = base.archivesName.get()
				from components.java
			}
		}
		
		repositories {
			// Add repositories to publish to here.
		}
	}
}